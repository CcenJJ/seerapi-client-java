import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'checkstyle'
    id "net.ltgt.errorprone" version "0.8.1"
    id "com.github.spotbugs" version "1.6.10"
    id 'com.bmuschko.nexus' version '2.3.1'
    id "io.codearte.nexus-staging" version "0.20.0"
    id 'com.adarshr.test-logger' version '1.7.0'
}

group = 'com.imsweb'
version = '3.15'
description = 'Java client library for SEER*API'

println "Starting build using ${Jvm.current()}"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.3.3")
    errorproneJavac("com.google.errorprone:javac:2.3.3")

    compile 'com.squareup.retrofit2:retrofit:2.6.1'
    compile 'com.squareup.retrofit2:converter-jackson:2.6.1'

    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:3.11.1'
}

jar {
    // specify the archive name; otherwise the version is appended to the war file
    archiveName = 'seerapi-java.jar'

    manifest {
        attributes('Implementation-Title': 'SEER*API Java Client',
                'Implementation-Version': version,
                'Implementation-Vendor': group,
                'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.seerapi.client'
        )
    }
}

testlogger {
    theme 'mocha'
}

checkstyle {
    toolVersion '8.23'
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/suppressions.xml')]
    configFile = file("config/checkstyle/checkstyle.xml")
}

spotbugs {
    toolVersion = '3.1.12'
    excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
}
wrapper {
    gradleVersion = '5.6'
    distributionType = Wrapper.DistributionType.ALL
}

modifyPom {
    project {
        name 'SEER*API Java Client'
        description 'API mapping for SEER*API in Java'
        url 'https://github.com/imsweb/seerapi-client-java'
        inceptionYear '2014'

        scm {
            url 'https://github.com/imsweb/seerapi-client-java'
            connection 'scm:https://github.com/imsweb/seerapi-client-java.git'
            developerConnection 'scm:git@github.com:imsweb/seerapi-client-java.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ctmay4'
                name 'Chuck May'
                email 'mayc@imsweb.com'
            }
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}
