apply plugin: 'com.jfrog.bintray'

// simple task to return the version
task getVersion << {
    println version
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenStuff(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = bintrayUser // comes from gradle.properties file in ~/.gradle
    key = bintrayKey   // comes from gradle.properties file in ~/.gradle
    publications = ['mavenStuff']
    publish = true
    pkg {
        repo = 'seer'
        userOrg = 'imsweb'
        name = 'seerapi-client-java'
        desc = 'API mapping for SEER*API in Java'
        licenses = ['MIT']
        labels = ['seerapi', 'imsweb']

        websiteUrl = 'https://github.com/imsweb/seerapi-client-java'
        issueTrackerUrl = 'https://github.com/imsweb/seerapi-client-java/issues'
        vcsUrl = 'https://github.com/imsweb/seerapi-client-java.git'
    }
}